2
FBGUI
.bas
CWindow: COMMCTRL: ComboBox
' ########################################################################################
' Microsoft Windows
' File: CW_COMMCTRL_ComboBox.fbtpl - Template
' Contents: Demonstrates the use of the ComboBox control.
' Comments: Commonly used to allow a user to select an item from a predefined list.
' Compiler: FreeBasic 32 & 64 bit
' Copyright (c) 2016 José Roca. Freeware. Use at your own risk.
' THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
' EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE IMPLIED WARRANTIES OF
' MERCHANTABILITY AND/OR FITNESS FOR A PARTICULAR PURPOSE.
' ########################################################################################

#INCLUDE ONCE "Afx/CWindow.inc"
USING Afx.CWindowClass

CONST IDC_COMBOBOX = 1001

DECLARE FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                          BYVAL hPrevInstance AS HINSTANCE, _
                          BYVAL szCmdLine AS ZSTRING PTR, _
                          BYVAL nCmdShow AS LONG) AS LONG

   END WinMain(GetModuleHandleW(NULL), NULL, COMMAND(), SW_NORMAL)

' ========================================================================================
' Window procedure
' ========================================================================================
FUNCTION WndProc (BYVAL hWnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

   SELECT CASE uMsg

      CASE WM_COMMAND
         SELECT CASE LOWORD(wParam)
            ' // If ESC key pressed, close the application sending an WM_CLOSE message
            CASE IDCANCEL
               IF HIWORD(wParam) = BN_CLICKED THEN
                  SendMessageW hwnd, WM_CLOSE, 0, 0
                  EXIT FUNCTION
               END IF
            CASE IDC_COMBOBOX
               ' // The selection has changed
               IF HIWORD(wParam) = LBN_SELCHANGE THEN
                  ' // Handle of the combobox
                  DIm hCombobox AS HWND = GetDlgItem(hwnd, IDC_COMBOBOX)
                  ' // Retrieve the Item selected
                  DIM curSel AS LONG = SendMessageW(hCombobox, CB_GETCURSEL, 0, 0)
                  ' // Retrieve the lenght of the text of the selected item
                  DIM nLen AS LONG = SendMessageW(hComboBox, CB_GETLBTEXTLEN, curSel, 0)
                  ' // Retrieve the text of the selected item
                  DIM pwszText AS WSTRING PTR = CALlocate(nLen + 1, SIZEOF(WCHAR))
                  SendMessageW(hComboBox, CB_GETLBTEXT, curSel, CAST(LPARAM, pwszText))
                  MessageBoxW hwnd, "You have selected " & *pwszText, "ComboBox Test", MB_OK
                  Deallocate pwszText
                  pwszText = NULL
               END IF
         END SELECT

    	CASE WM_DESTROY
         PostQuitMessage(0)
         EXIT FUNCTION

   END SELECT

   FUNCTION = DefWindowProcW(hWnd, uMsg, wParam, lParam)

END FUNCTION
' ========================================================================================

' ========================================================================================
' Main
' ========================================================================================
FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                  BYVAL hPrevInstance AS HINSTANCE, _
                  BYVAL szCmdLine AS ZSTRING PTR, _
                  BYVAL nCmdShow AS LONG) AS LONG

   ' // Set process DPI aware
   AfxSetProcessDPIAware

   DIM pWindow AS CWindow
   pWindow.Create(NULL, "ComboBox control", @WndProc)
   pWindow.Center

   ' // Add a combobox control
   DIM hCombobox AS HWND = pWindow.AddControl("ComboBox", , IDC_COMBOBOX, "", 80, 30, 190, 100)

   ' // Fill the control with some data
   DIM i AS LONG = 1, wszText AS WSTRING * 260
   FOR i = 1 TO 9
      wszText = "Item " & RIGHT("00" & STR(i), 2)
      SendMessageW(hComboBox, CB_ADDSTRING, 0, CAST(LPARAM, @wszText))
   NEXT

   |

   FUNCTION = pWindow.DoEvents(nCmdShow)

END FUNCTION
' ========================================================================================

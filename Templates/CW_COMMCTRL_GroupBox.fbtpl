2
FBGUI
.bas
CWindow: COMMCTRL: Group Box
' ########################################################################################
' Microsoft Windows
' File: CW_COMMCTRL_GroupBox.fbtpl - Template
' Contents: Demonstrates the use of the GroupBox control.
' Comments: Drawn around several controls to indicate a visual association between them.
' Often used around related radio buttons.
' Remarks: In PowerBASIC it is mistakenly called "Frame" instead of "Group Box".
' Compiler: FreeBasic 32 & 64 bit
' Copyright (c) 2016 José Roca. Freeware. Use at your own risk.
' THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
' EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE IMPLIED WARRANTIES OF
' MERCHANTABILITY AND/OR FITNESS FOR A PARTICULAR PURPOSE.
' ########################################################################################

#INCLUDE ONCE "Afx/CWindow.inc"
USING Afx.CWindowClass

CONST IDC_GROUPBOX = 1001
CONST IDC_LABEL = 1002
CONST IDC_CHECK3STATE = 1003
CONST IDC_EDIT = 1004
CONST IDC_BUTTON = 1005

DECLARE FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                          BYVAL hPrevInstance AS HINSTANCE, _
                          BYVAL szCmdLine AS ZSTRING PTR, _
                          BYVAL nCmdShow AS LONG) AS LONG

   END WinMain(GetModuleHandleW(NULL), NULL, COMMAND(), SW_NORMAL)

' ========================================================================================
' Window procedure
' ========================================================================================
FUNCTION WndProc (BYVAL hWnd AS HWND, BYVAL uMsg AS UINT, BYVAL wParam AS WPARAM, BYVAL lParam AS LPARAM) AS LRESULT

   SELECT CASE uMsg

      CASE WM_COMMAND
         SELECT CASE LOWORD(wParam)
            ' // If ESC key pressed, close the application sending an WM_CLOSE message
            CASE IDCANCEL
               IF HIWORD(wParam) = BN_CLICKED THEN
                  SendMessageW hwnd, WM_CLOSE, 0, 0
                  EXIT FUNCTION
               END IF
            CASE IDC_BUTTON
               IF HIWORD(wParam) = BN_CLICKED THEN
                  SendMessageW hwnd, WM_CLOSE, 0, 0
                  EXIT FUNCTION
               END IF
         END SELECT

      CASE WM_CTLCOLORSTATIC
         ' // Draw the GroupBox using the main window background color
         IF GetDlgCtrlID(CAST(HWND, lParam)) = IDC_GROUPBOX THEN
            ' Return handle of brush used to paint background
            FUNCTION = CAST(LRESULT, GetClassLongPtr(GetParent(hwnd), GCLP_HBRBACKGROUND))
            EXIT FUNCTION
         END IF

    	CASE WM_DESTROY
         PostQuitMessage(0)
         EXIT FUNCTION

   END SELECT

   FUNCTION = DefWindowProcW(hWnd, uMsg, wParam, lParam)

END FUNCTION
' ========================================================================================

' ========================================================================================
' Main
' ========================================================================================
FUNCTION WinMain (BYVAL hInstance AS HINSTANCE, _
                  BYVAL hPrevInstance AS HINSTANCE, _
                  BYVAL szCmdLine AS ZSTRING PTR, _
                  BYVAL nCmdShow AS LONG) AS LONG

   ' // Set process DPI aware
   AfxSetProcessDPIAware

   DIM pWindow AS CWindow
   pWindow.Brush = GetStockObject(WHITE_BRUSH)
   pWindow.Create(NULL, "GroupBox example", @WndProc)
   pWindow.Center

   pWindow.AddControl("GroupBox", , IDC_GROUPBOX, "GroupBox", 20, 20, 305, 100)
   pWindow.AddControl("Label", , IDC_LABEL, "Label", 60, 50, 75, 23)
   pWindow.AddControl("Check3State", , IDC_CHECK3STATE, "Click me", 60, 80, 75, 23)
   pWindow.AddControl("Edit", , IDC_EDIT, "", 210, 50, 75, 23)
   pWindow.AddControl("Button", , IDC_BUTTON, "&Close", 210, 80, 75, 23)

   |

   FUNCTION = pWindow.DoEvents(nCmdShow)

END FUNCTION
' ========================================================================================
